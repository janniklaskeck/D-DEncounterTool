plugins {
    id "java"
    id "distribution"
    id "eclipse"
    id "org.sonarqube" version "2.5"
    id "edu.sc.seis.launch4j" version "2.4.1"
    id "com.github.johnrengelman.shadow" version "2.0.0"
    id "com.github.hierynomus.license" version "0.14.0"
}

group = 'app.bvk'
version = '1.0.0'

description = "DnDEncounterTool"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
}

ext {
    mainClass = "app.bvk.EncounterTool"
    fontawesomeVersion = "8.15"
    controlsfxVersion = "8.40.13"
    gsonVersion = "2.8.1"
    slf4jVersion = "1.7.25"
    log4jVersion = "2.8.2"
    truezipVersion = "7.7.10"
    junitVersion = "4.12"
}

dependencies {
    compile "de.jensd:fontawesomefx-commons:$fontawesomeVersion"
    compile "org.controlsfx:controlsfx:$controlsfxVersion"
    compile "com.google.code.gson:gson:$gsonVersion"
    compile "org.slf4j:slf4j-api:$slf4jVersion"
    compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    compile "org.apache.logging.log4j:log4j-jul:$log4jVersion"
    compile "de.schlichtherle.truezip:truezip-driver-zip:$truezipVersion"
    compile "de.schlichtherle.truezip:truezip-file:$truezipVersion"
    compile "de.schlichtherle.truezip:truezip-path:$truezipVersion"
    testCompile "junit:junit:$junitVersion"
}

jar {
    manifest {
        attributes "Implementation-Title": "DnDEncounterTool FatJar",
          "Implementation-Version": version,
          "Main-Class": "$mainClass"
    }
    appendix = "nolib"
}

shadowJar {
	classifier = null
}

distributions {
    main {
        contents {
            from shadowJar
        }
    }
}

launch4j {
    outputDir = "distributions"
    mainClassName = "$mainClass"
    icon = "$buildDir/resources/main/icon.ico"
    copyConfigurable = project.tasks.shadowJar.outputs.files
    jar = "lib/${project.tasks.shadowJar.archiveName}"
    supportUrl = "https://github.com/westerwave/D-DEncounterTool"
    copyright = "MIT LICENSE"
    language = "ENGLISH_US"
    initialHeapSize = 64
    maxHeapSize = 512
    jvmOptions = ["-Dfile.encoding=UTF-8"]
}
